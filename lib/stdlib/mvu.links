### VDOM STUFF ###
open import MvuAttrs;
open import MvuEvents;
open import MvuHTML;
open import MvuSubscriptions;
open import MvuCommands;

# Needed to ensure that virtual-dom is open
module VirtualDom {
  alien javascript "/lib/virtual-dom.js" {
    dummy : ();
  }
}

# Loads the "vdom.js" file, exposes runDom and updateDom.
module VDom {
  alien javascript "/lib/vdom.js" {
    runDom : forall a :: Type(Any, Any) .
      (String, HTML(a), AP(?a.End), Sub(a)) ~> ();
    updateDom : forall a :: Type(Any, Any) . (HTML(a), Sub(a)) ~> ();
    transition : forall a :: Type(Any, Any) . (AP(?a.End), HTML(a), Sub(a)) ~> ();
  }
}

typename Transition(model :: Type(Any, Any), msg :: Type(Any, Any)) =
    [| NoTransition:(model, Command(msg))
     | Transition: () {}~@ () |];


sig noTransition :
  forall
    model :: Type(Any, Any),
    msg :: Type(Any, Any) .
  (model, Command(msg)) ~> Transition(model, msg)
fun noTransition(model, cmd) {
  NoTransition(model, cmd)
}


sig applyLinearTransition : forall a :: Type(Any, Any) . (() {}~@ ()) {}~> ()
fun applyLinearTransition(f) { f() }

# Main event loop
sig evtLoop :
 forall msg :: Type(Any, Any),
        model :: Type(Any, Any),
        unrModel :: Type(Unl, Any) .
  (AP(?msg.End),
   model,
   (unrModel) {}~> HTML(msg),
   (msg, model) {}~> Transition(model, msg),
   (unrModel) {}~> Sub(msg),
   (model) {}~> (model, unrModel),
   Sub(msg)) {}~> ()
fun evtLoop(ap, model, view, updt, subscriptionsFn, extract, prevSubscriptions) {
  var (message, s) = receive(accept(ap));
  close(s);
  var updtRes = updt(message, model);
  switch(updtRes) {
    case NoTransition(model, cmd) ->
      processCommand(cmd, ap);
      # Extract unrestricted model
      var (model, unrModel) = extract(model);
      # Get new subscriptions
      var newSubscriptions = subscriptionsFn(unrModel);
      # Update DOM
      VDom.updateDom(view(unrModel), newSubscriptions);
      evtLoop(ap, model, view, updt, subscriptionsFn, extract, newSubscriptions)
    case Transition(fn) -> applyLinearTransition(fn)
  }
}

sig transition :
 forall a :: Type(Any, Any),
        b :: Type(Any, Any),
        msg :: Type(Any, Any),
        model :: Type(Any, Any),
        unrModel :: Type(Unl, Any) .
   (model,
   (unrModel) {}~> HTML(msg),
   (msg, model) {}~> Transition(model, msg),
   (unrModel) {}~> Sub(msg),
   (model) {}~> (model, unrModel),
   Command(msg)) {}~> Transition(a, b)
fun transition(model, view, updt, sub, extract, cmd) {
  Transition(linfun() {
    ignore(spawn {
       # Create new AP used for next message type
       var ap = new();
       processCommand(cmd, ap);
       # Extract unrestricted model
       var (model, unrModel) = extract(model);
       # Get new subscriptions
       var newSubscriptions = sub(unrModel);
       # FFI call
       VDom.transition(ap, view(unrModel), newSubscriptions);
       evtLoop(ap, model, view, updt, sub, extract, newSubscriptions)
    })})
}

sig runLinear:
  forall
  msg::Type(Any, Any), model::Type(Any, Any),
    unrModel::Type(Unl, Any) .
  (String,
   model,
   (unrModel) {}~> HTML(msg),
   (msg, model) {}~> Transition(model, msg),
   (unrModel) {}~> Sub(msg),
   (model) {}~> (model, unrModel)
   ) {}~> ()
fun runLinear(placeholder, model, view, updt, subscriptions, extract) {
  var evtHandler = spawnClient {
    var (model, unrModel) = extract(model);
    var ap = new();
    VDom.runDom(placeholder, view(unrModel), ap, subscriptions(unrModel));
    evtLoop(ap, model, view, updt, subscriptions, extract, subscriptions(unrModel))
  };
  ()
}

# User-facing function (assuming an unrestricted model)
sig run:
  forall
    msg :: Type(Any, Any),
    model :: Type(Unl, Any) .
  (String,
    model,
    (model) {}~> HTML(msg),
    (msg, model) {}~> (model, Command(msg)),
    (model) {}~> Sub(msg)) {}~> ()
fun run(placeholder, model, view, updt, subscriptions) {
  fun wrapUpdt(msg, model) {
    var (model, cmd) = updt(msg, model);
    NoTransition(model, cmd)
  }
  runLinear(placeholder, model, view, wrapUpdt, subscriptions, fun(x) { (x, x) })
}

sig runSimple :
  forall
    msg :: Type(Any, Any),
    model :: Type(Unl, Any) .
  (String,
    model,
    (model) {}~> HTML(msg),
    (msg, model) {}~> model) {}~> ()
fun runSimple(placeholder, model, view, updt) {
  run(placeholder, model, view,
    fun(msg, model) { (updt(msg, model), NoCommand)},
    fun(_) { SubEmpty } )
}

sig runStatic :
  forall
    model :: Type(Unl, Any) .
  (String, model, (model) {}~> HTML(())) {}~> ()
fun runStatic(placeholder, model, view) {
  runSimple(placeholder, model, view, fun(_, model) { model })
}
