### VDOM STUFF ###

# Doc Type

typename VirtualHandler(a :: Type(Any, Any)) =
  [| StringHandler: (String, (String) {}~> a)
   | UnitHandler: (String, () {}~> a)
   | TupleIntHandler: (String, ((Int,Int)) {}~> a)
   | TimeHandler: (Int, () {}~> a)
   | AnimationFrameHandler: ((a) {}~> a) |];

##### GO FROM HERE
# For some reason, it doesn't seem possible to have a Doc parameterised
# over a linear type variable. This is because when trying to unify the
# type variable, the flexible type variable appears to be unrestricted,
# whereas the specified type is linear.
# TODO: Next up, minimal failing example, to demonstrate the problem further.
typename Doc(a :: Type(Any, Any)) =
  [| DocTextNode: (String)
   | DocTagNode:
      (tagName: String, attrs: [(String, String)],
       eventHandlers: [VirtualHandler(a)],
       children: [Doc(a)])
   |];

# Needed to ensure that virtual-dom is open
module VirtualDom {
  alien javascript "/lib/virtual-dom.js" {
    dummy : ();
  }
}

typename Sub(a :: Type(Any, Any)) =
  [| EmptySub
   | HandlerSub : (String, VirtualHandler(a))
   | Sub : (a) |];

# Loads the "vdom.js" file, exposes runDom and updateDom.
module VDom {
  alien javascript "/lib/vdom.js" {
    runDom : forall a :: Type(Any, Any) .
      (String, Doc(a), Process({wild}), [Sub(a)]) ~%~> ();
    updateDom : forall a :: Type(Any, Any) . (Doc(a)) ~%~> ();
    updateSubscriptions : forall a :: Type(Any, Any) . (([Sub(a)],[Sub(a)])) ~%~> ();
    getUniqueID : forall a :: Type(Any, Any) . (VirtualHandler(a)) ~%~> (String);
    # diffSubs : forall a . (VirtualHandler(a)) ~%e~> (String);
  }
}
### SUBSCRIPTIONS ###


fun uniqueID(hdlr) {
  VDom.getUniqueID(hdlr)
}

fun getKeyDownCode(f) {
  var hdler = StringHandler("keydown", f);
  HandlerSub(uniqueID(hdler), hdler)
}

fun getKeyUpCode(f) {
  var hdler = StringHandler("keyup", f);
  HandlerSub(uniqueID(hdler), hdler)
}

fun getKeyPressCode(f) {
  var hdler = StringHandler("keypress", f);
  HandlerSub(uniqueID(hdler), hdler)
}

fun getMousePos(f) {
  var hdler = TupleIntHandler("mousemove", f);
  HandlerSub(uniqueID(hdler), hdler)
}

fun getEvery(interval, f) {
  var hdler = TimeHandler(interval, f);
  HandlerSub(uniqueID(hdler), hdler)
}

fun animationFrame(f) {
  var hdler = UnitHandler("animationFrame", f);
  HandlerSub(uniqueID(hdler), hdler)
}

######

sig textDoc : forall a :: Type(Any, Any) . (String) ~> Doc(a)
fun textDoc(str) {
  DocTextNode(str)
}

sig tag : forall a :: Type(Any, Any) .
  (String, [(String, String)], [VirtualHandler(a)], [Doc(a)]) -> Doc(a)
fun tag(nameOfTag, attrsOfTag, handlers, childrenOfTag) {
  DocTagNode((tagName=nameOfTag, attrs=attrsOfTag, eventHandlers=handlers, children=childrenOfTag))
}

fun attr(k, v) {
  (k, v)
}

# TODO: Put into a module
typename Command(a :: Type(Any, Any)) =
#typename Command(a) =
  [| NoCommand | Spawn: (() {}~> a) | SpawnLinear: (() {}~@ a) |];

#
# sig go : forall a :: Type(Any, Any) . (Command(a), AP(?a.End)) {}~> ()
# fun go(data, ap) {
#   var toSend = switch (data) {
#     case Apply(f) -> apply(f)
#     case ApplyLinear(f) -> applyLinear(f)
#   };
#   var _ = send(toSend, request(ap));
#   ()
# }
#

sig applySpawn: forall a :: Type(Any, Any) . (() {}~> a) {}~> a
fun applySpawn(f) { f() }

sig applySpawnLinear : forall a :: Type(Any, Any) . (() {}~@ a) {}~> a
fun applySpawnLinear(f) { f() }

# TODO: Generalise to monoidal interface
# TODO: We need the annotation on the function...
sig processCommand :
  forall msg :: Type(Any, Any) . (Command(msg), AP(?msg.End)) ~> ()
fun processCommand(cmd, ap) {
  switch(cmd) {
    case NoCommand -> ()
    case Spawn(f) ->
      ignore(
        spawn {
          var _ = send(applySpawn(f), request(ap));
          ()
        })
    case SpawnLinear(f) ->
      ignore(
        spawn {
          var _ = send(applySpawnLinear(f), request(ap));
          ()
        })
  }
}

# Main event loop
sig evtLoop :
 forall msg :: Type(Any, Any),
        model :: Type(Any, Any),
        unrModel :: Type(Unl, Any) .
  (AP(?msg.End),
   (msg, model) ~%~> (model, Command(msg)),
   (model) ~%~> (model, unrModel),
   (unrModel) ~%~> Doc(msg),
   model,
   [Sub(msg)],
   (unrModel) ~%~> [Sub(msg)]) ~%~> ()
fun evtLoop(ap, updt, extract, render, model, prevSubscriptions, subscriptionsFn) {
  var (message, _) = receive(accept(ap));
  var (model, cmd) = updt(message, model);
  processCommand(cmd, ap);
  # Update DOM
  var (model, unrModel) = extract(model);
  # Get new subscriptions
  var newSubscriptions = subscriptionsFn(unrModel);
  # Update subscriptions on the DOM (FFI call)
  VDom.updateSubscriptions((prevSubscriptions, newSubscriptions));
  # Update DOM
  VDom.updateDom(render(unrModel));
  # Loop with new subscriptions
  evtLoop(ap, updt, extract, render, model, newSubscriptions, subscriptionsFn)
}

sig runLinearVDom :
  forall
    msg::Type(Any, Any),
    model::Type(Any, Any),
    unrModel::Type(Unl, Any) .
  (String,
   (unrModel) ~%~> Doc(msg),
   (msg, model) ~%~> (model, Command(msg)),
   (model) ~%~> (model, unrModel),
   model,
   (unrModel) ~%~> [Sub(msg)]
   ) ~%~>
   Process({ wild })
fun runLinearVDom(placeholder, view, updt, extract, model, subscriptions) {
  var (model, unrModel) = extract(model);
  var evtHandler = spawnClient {
    var ap = new();
    evtLoop(ap, updt, extract, view, model, subscriptions(unrModel), subscriptions)
  };
  var _ = spawnClient { VDom.runDom(placeholder, view(unrModel), evtHandler, subscriptions(unrModel)) };
  evtHandler
}

# User-facing function (assuming an unrestricted model)
# fun runVDom(placeholder, view, updt, model, subscriptions) {
#   runLinearVDom(placeholder, view, updt, fun(x) { (x, x) },
#     model, subscriptions)
# }

